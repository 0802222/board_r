plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.cho'
version = '0.1.0'   // 0.0.1-SNAPSHOT
description = 'Board'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    //dot env
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // DB driver
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'com.h2database:h2'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // mockito
    testImplementation 'org.mockito:mockito-core:5.2.0'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // email
    implementation 'org.springframework.boot:spring-boot-starter-mail'
}


// Java Code Coverage (테스트 커버리지 측정)
jacoco {
    toolVersion = "0.8.11"
}

// 테스트 후 자동으로 리포트 생성
tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// JaCoCo 리포트 설정
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true   // Codecov 용
        html.required = true  // local 확인용
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test')
    }
}

// 커버리지 검증
jacocoTestCoverageVerification {
    violationRules {
        rule {
            // 제외할 클래스들
            excludes = [
                    '**.dto.**',
                    '**.entity.**',
                    '**.config.**',
                    '**.BoardApplication',
                    '**.exception.**'
            ]

            limit {
                minimum = 0.30 // 추후 0.60 수준으로 변경 예정
            }
        }
    }
}